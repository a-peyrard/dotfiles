# replace history search by a nice fzf widget

# Register function as widget.
eval zle -N custom-fzf-launch-from-history

# Select command from history into the command line.
function custom-fzf-launch-from-history() {
	if (! hash fzf &>/dev/null) {
		return 1
	}

	setopt LOCAL_OPTIONS NO_GLOB_SUBST NO_POSIX_BUILTINS PIPE_FAIL 2>/dev/null

	local selected=(
		$(
			fc -l 1 \
				| fzf \
					--tac \
					--nth='2..,..' \
					--tiebreak='index' \
					--query="${LBUFFER}" \
					--exact \
					--prompt='$ '
		)
	)

	local stat=$?

	if [[ "$selected" != '' ]] {
		local num=$selected[1]

		if [[ "$num" != '' ]] {
			zle vi-fetch-history -n $num
		}
	}

	zle redisplay

	if (typeset -f zle-line-init &>/dev/null) {
		zle zle-line-init
	}

	return $stat
}

# Last but not least, bind the widget
bindkey '^r' custom-fzf-launch-from-history # Select command from history into the command line.