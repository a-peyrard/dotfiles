# zsh-vi-mode plugin configuration
# Must load BEFORE fzf-tab (90) and syntax-highlighting (99)

ZVM_DIR="$HOME/.zsh/plugins/zsh-vi-mode"

# Skip if not installed
[[ ! -f "$ZVM_DIR/zsh-vi-mode.plugin.zsh" ]] && return

# Configuration (set before sourcing)
ZVM_VI_INSERT_ESCAPE_BINDKEY=jk
ZVM_LINE_INIT_MODE=$ZVM_MODE_INSERT
ZVM_CURSOR_STYLE_ENABLED=false
ZVM_READKEY_ENGINE=$ZVM_READKEY_ENGINE_NEX
ZVM_ESCAPE_KEYTIMEOUT=0.03

# Custom cursor handling (shape + color)
# \e[2 q = block cursor, OSC 12 = cursor color
function zvm_after_select_vi_mode() {
    case $ZVM_MODE in
        $ZVM_MODE_NORMAL)
            print -n $'\e[2 q\e]12;#50fa7b\a' > /dev/tty
            ;;
        $ZVM_MODE_INSERT)
            print -n $'\e[2 q\e]12;#f8f8f2\a' > /dev/tty
            ;;
        $ZVM_MODE_VISUAL|$ZVM_MODE_VISUAL_LINE)
            print -n $'\e[2 q\e]12;#50fa7b\a' > /dev/tty
            ;;
    esac
}

# Hook to restore bindings after zvm initializes
function zvm_after_init() {
    # Re-enable fzf if installed
    [[ -f ~/.fzf.zsh ]] && source ~/.fzf.zsh

    # History prefix search with arrow keys in insert mode
    autoload -U up-line-or-beginning-search down-line-or-beginning-search
    zle -N up-line-or-beginning-search
    zle -N down-line-or-beginning-search
    bindkey -M viins '^[[A' up-line-or-beginning-search
    bindkey -M viins '^[[B' down-line-or-beginning-search

    # Restore emacs-style bindings in insert mode
    bindkey -M viins '^A' beginning-of-line
    bindkey -M viins '^E' end-of-line
    bindkey -M viins '^K' kill-line
    bindkey -M viins '^U' backward-kill-line
    bindkey -M viins '^W' backward-kill-word
    bindkey -M viins '^R' custom-fzf-launch-from-history 2>/dev/null || bindkey -M viins '^R' history-incremental-search-backward
    bindkey -M viins '^P' up-line-or-history
    bindkey -M viins '^N' down-line-or-history

    # Handle bracketed paste - switch to insert mode for paste
    # This prevents pasted text from being interpreted as vi commands
    function bracketed-paste-begin() {
        zle vi-insert 2>/dev/null  # Enter insert mode if in normal mode
        zle .bracketed-paste
    }
    zle -N bracketed-paste bracketed-paste-begin
}

# Hook to restore history prefix search in normal mode
function zvm_after_lazy_keybindings() {
    # History prefix search with j/k in normal mode
    autoload -U up-line-or-beginning-search down-line-or-beginning-search
    zle -N up-line-or-beginning-search
    zle -N down-line-or-beginning-search

    bindkey -M vicmd 'k' up-line-or-beginning-search
    bindkey -M vicmd 'j' down-line-or-beginning-search
}

# Source the plugin
source "$ZVM_DIR/zsh-vi-mode.plugin.zsh"
